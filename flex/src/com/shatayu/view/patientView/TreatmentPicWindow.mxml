<?xml version="1.0" encoding="utf-8"?>
<mv:ResizableTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" width="800" height="700"
	xmlns:mv = "com.shatayu.view.patientView.*" showCloseButton="true" close="{PopUpManager.removePopUp(this)}">
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.controls.Label;
			import com.shatayu.controller.PatientMgmtControllerEvents;
			import com.shatayu.event.ShatayuEvent;
			import mx.collections.ArrayCollection;
			import com.shatayu.model.Treatment;
			import com.shatayu.model.TreatmentPhotos;
			import mx.managers.PopUpManager;
			import mx.controls.Image;
			import mx.graphics.codec.JPEGEncoder;
			import flash.display.Bitmap;
			
			private var video:Video = null;
			private var bitmapData:BitmapData = null;
			private var bitMap:Bitmap =null;
			private var picCount:int = 0;
			
			[Bindable]
			public var treatment:Treatment;
			
			
			
			private function onCreation():void{
				var cam:Camera = Camera.getCamera();
				video = new Video(740,580);
				video.attachCamera(cam);
				video.x =20;
				video.y =60;
				
				capPic.rawChildren.addChild(video);
			    bitmapData =  new BitmapData(video.width,video.height);
			 	bitMap = new Bitmap(bitmapData);
				bitMap.x = 20;
				bitMap.y = 60;
				capPic.rawChildren.addChild(bitMap);
				
				
			}
			
			private function saveImage():void{
				var jpg:JPEGEncoder = new JPEGEncoder();
				var byteArray:ByteArray = jpg.encode(bitmapData);
				
				var trPicWin:TreatmentPhotos = new TreatmentPhotos();
				trPicWin.treatment = treatment;
				trPicWin.imageData = byteArray;
				trPicWin.description = descr.text;
				var arr:ArrayCollection = new ArrayCollection();
				arr.addItem(trPicWin);
				var event:ShatayuEvent = new ShatayuEvent(PatientMgmtControllerEvents.SAVE_TREATMENT_PICS_COMMAND,arr,onImageSaved);
        		event.dispatch();
			}
			
			private function onImageSaved(obj:Object):void{
				capPic.rawChildren.setChildIndex(video,(capPic.rawChildren.numChildren -1));
				appendImageList(obj as ArrayCollection);
			}
			
			private function appendImageList(arr:ArrayCollection):void{
				
				for each(var trPic:TreatmentPhotos in arr){
					addImage(trPic.imageData,trPic.description);
				}
			}
			
			private function capturePic():void{
				capPic.rawChildren.setChildIndex(bitMap,(capPic.rawChildren.numChildren -1));
				bitmapData.draw(video);
			}
			
			private function newImage():void{
				capPic.rawChildren.setChildIndex(video,(capPic.rawChildren.numChildren -1));
			}
			
			private function onGetPic():void{
				
				var event:ShatayuEvent = new ShatayuEvent(PatientMgmtControllerEvents.GET_TREATMENT_PICS_COMMAND,treatment,onImagesRecieved);
        		event.dispatch();
			}
			
			private function onImagesRecieved(obj:Object):void{
				
				appendImageList(obj as ArrayCollection);
			}
			
			private function addImage(picsData:ByteArray,descr:String):void{
				var vbox:VBox = new VBox();
				var img:Image = new Image();
				vbox.percentHeight = 100;
				vbox.percentWidth = 100;
				vbox.setStyle("verticalAlign","middle");
				vbox.setStyle("horizontalAlign","center");
				img.toolTip = descr;
				img.source =  picsData;
				vbox.addChild(img);
				picView.addChild(vbox);
			}
			
			
			private function onNextPic():void{
				if(picCount >= (picView.numChildren -1)){
					return;
				}
				picCount = picCount + 1;
				picView.selectedIndex = picCount;
			}
			
			private function onPrevPic():void{
				if(picCount == 0){
					return;
				}
				picCount = picCount - 1;
				picView.selectedIndex = picCount;
				
			}
		]]>
	</mx:Script>
	<mx:TabNavigator width= "100%" height="100%" >
		
		
		<mx:VBox width= "100%" height="100%" label="Watch Picture" creationComplete="onGetPic()">
			<mx:ViewStack id= "picView" width= "100%" height="100%">
				
			</mx:ViewStack>
			<mx:HBox>
				<mx:Button label="Next" click = "onNextPic()"/>
				<mx:Button label="Previous" click = "onPrevPic()"/>
			</mx:HBox>
		</mx:VBox>
		
		<mx:VBox id = "capPic" width= "100%" height="100%" label="Take Picture" creationComplete="onCreation()">
			<mx:HBox>
				<mx:Button label="Save Pic" click="saveImage()"/>
				<mx:Button label="New Pic" click="newImage()"/>
				<mx:Button label="Capture Pic" click="capturePic()"/>
			</mx:HBox>
			<mx:FormItem label="Description">
				<mx:TextInput id = "descr" />
			</mx:FormItem>
		</mx:VBox>
		
	</mx:TabNavigator>
	
</mv:ResizableTitleWindow>
